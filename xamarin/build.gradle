import java.nio.file.Path

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

String msbuildPath = '/Library/Frameworks/Mono.framework/Versions/Current/Commands/msbuild'
Path projectPath = new File('../../XamarinDependency').toPath()
Path appPath = projectPath.resolve('XamarinDependency.Application')
Path objPath = appPath.resolve('obj')

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        debug {
            java.srcDirs objPath.resolve('Debug/android/src')
            resources.srcDirs objPath.resolve('Debug/android/bin/unpacked')
        }
        release {
            java.srcDirs objPath.resolve('Release/android/src')
            resources.srcDirs objPath.resolve('Release/android/bin/unpacked')
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    debugImplementation files(objPath.resolve('Debug/android/bin/mono.android.jar'))
    releaseImplementation files(objPath.resolve('Release/android/bin/mono.android.jar'))

    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

task cleanXamarin(type: Exec) {
    workingDir appPath

    executable msbuildPath

    args '/target:clean'

    inputs.dir(projectPath)

    outputs.dirs(objPath.resolve('Debug'))
}

task compileDebugXamarin(type: Exec) {
    workingDir appPath

    executable msbuildPath

    args '/target:BuildApk', '/property:Configuration=Debug', '/property:Platform=AnyCPU'

    inputs.dir(projectPath)

    outputs.dirs(objPath.resolve('Debug'))
}

task compileReleaseXamarin(type: Exec) {
    workingDir appPath

    executable msbuildPath

    args '/target:BuildApk', '/property:Configuration=Release', '/property:Platform=AnyCPU'

    inputs.dir(projectPath)

    outputs.dirs(objPath.resolve('Release'))
}

task unpackDebugXamarin(type: UnpackXamarinApk) {
    apk file(objPath.resolve('Debug/android/bin/com.roydammarell.XamarinDependency.apk'))

    dependsOn compileDebugXamarin
}

task unpackReleaseXamarin(type: UnpackXamarinApk) {
    apk file(objPath.resolve('Release/android/bin/com.roydammarell.XamarinDependency.apk'))

    dependsOn compileReleaseXamarin
}

class UnpackXamarinApk extends Copy {
    def apk(File file) {
        File directory = file.parentFile
        Path outputDir = directory.toPath().resolve('unpacked')

        from project.zipTree(file).matching {
            include(['assemblies/**' ,'lib/**', 'typemap.**', 'environment', 'NOTICE'])
        }
        into outputDir
    }
}

afterEvaluate {
    tasks.clean.dependsOn cleanXamarin

    tasks.generateDebugSources.dependsOn unpackDebugXamarin
    tasks.generateReleaseSources.dependsOn unpackReleaseXamarin

    tasks.compileDebugKotlin.dependsOn unpackDebugXamarin
    tasks.compileReleaseKotlin.dependsOn unpackReleaseXamarin

    tasks.compileDebugJavaWithJavac.dependsOn unpackDebugXamarin
    tasks.compileReleaseJavaWithJavac.dependsOn unpackReleaseXamarin
}

repositories {
    mavenCentral()
}
